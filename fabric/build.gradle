plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    common(project(path: ":fabric-like", configuration: "namedElements")) { transitive false }
    shadowBundle(project(path: ":fabric-like", configuration: "transformProductionFabric"))

    modImplementation("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") { transitive = false }
    modImplementation("me.shedaniel.cloth:cloth-config:${rootProject.cloth_config_version}")
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") { transitive = false }

    modImplementation("com.misterpemodder:shulkerboxtooltip-fabric:${rootProject.shulker_box_tooltip_version}") { transitive false }

    //modImplementation "net.kyrptonaught:kyrptconfig:${project.kyrptconfig_version}"
    //modImplementation "net.kyrptonaught:shulkerutils:${project.shulkerutils_version}"
    //modCompileOnly modRuntimeOnly("curse.maven:quick-shulker-${rootProject.curse_quickshulker_file}:4592296") { transitive false }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'minecraft_version', rootProject.minecraft_version_min
    inputs.property 'fabric_api_version', rootProject.fabric_api_version_min
    inputs.property 'architectury_version', rootProject.architectury_api_version_min
    inputs.property 'cloth_config_version', rootProject.cloth_config_version_min
    inputs.property 'mod_menu_version', rootProject.mod_menu_version_min
    inputs.property 'shulker_box_tooltip_version', rootProject.shulker_box_tooltip_version_min

    filesMatching('fabric.mod.json') {
        expand (version: project.version,
                minecraft_version: rootProject.minecraft_version_min,
                fabric_api_version: rootProject.fabric_api_version_min,
                architectury_version: rootProject.architectury_api_version_min,
                cloth_config_version: rootProject.cloth_config_version_min,
                mod_menu_version: rootProject.mod_menu_version_min,
                shulker_box_tooltip_version: rootProject.shulker_box_tooltip_version_min)
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile

    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
